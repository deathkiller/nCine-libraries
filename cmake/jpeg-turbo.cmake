set(TARGET_JPEG jpeg)
set(URL_JPEG http://downloads.sourceforge.net/project/libjpeg-turbo/1.4.2/libjpeg-turbo-1.4.2.tar.gz)
set(URL_MD5_JPEG 86b0d5f7507c2e6c21c00219162c3c44)
set(LIBNAME_TURBOJPEG turbojpeg)
set(LIBNAME_JPEG jpeg7)
set(COMMON_CMAKE_ARGS_JPEG -DWITH_SIMD=FALSE -DWITH_JPEG7=TRUE -DWITH_JPEG8=FALSE)

if(MSVC)
	set(LIBNAME_TURBOJPEG_LIB turbojpeg-static) # for static linking
	set(LIBNAME_JPEG_LIBDYN jpeg)

	if(MSVC_IDE)
		set(LIBFILE_TURBOJPEG_NOEXT ${EP_BASE}/Build/project_${TARGET_JPEG}/${CONFIGURATION}/${LIBNAME_TURBOJPEG})
		set(LIBFILE_TURBOJPEG_LIB ${EP_BASE}/Build/project_${TARGET_JPEG}/${CONFIGURATION}/${LIBNAME_TURBOJPEG_LIB}.lib)
		set(LIBFILE_JPEG ${EP_BASE}/Build/project_${TARGET_JPEG}/sharedlib/${CONFIGURATION}/${LIBNAME_JPEG}.dll)
		set(LIBFILE_JPEG_LIBDYN ${EP_BASE}/Build/project_${TARGET_JPEG}/sharedlib/${CONFIGURATION}/${LIBNAME_JPEG_LIBDYN}.lib)
	else()
		set(LIBFILE_TURBOJPEG_NOEXT ${EP_BASE}/Build/project_${TARGET_JPEG}/${LIBNAME_TURBOJPEG})
		set(LIBFILE_TURBOJPEG_LIB ${EP_BASE}/Build/project_${TARGET_JPEG}/${LIBNAME_TURBOJPEG_LIB}.lib)
		set(LIBFILE_JPEG ${EP_BASE}/Build/project_${TARGET_JPEG}/sharedlib/${LIBNAME_JPEG}.dll)
		set(LIBFILE_JPEG_LIBDYN ${EP_BASE}/Build/project_${TARGET_JPEG}/sharedlib/${LIBNAME_JPEG_LIBDYN}.lib)
	endif()

	ExternalProject_Add(project_${TARGET_JPEG}
		URL ${URL_JPEG}
		URL_MD5 ${URL_MD5_JPEG}
		CMAKE_ARGS -DCMAKE_BUILD_TYPE=${CONFIGURATION} ${COMMON_CMAKE_ARGS_JPEG}
		BUILD_IN_SOURCE 0
		INSTALL_COMMAND ${CMAKE_COMMAND} -E copy_if_different ${LIBFILE_TURBOJPEG_NOEXT}.dll ${BINDIR}
			COMMAND ${CMAKE_COMMAND} -E copy_if_different ${LIBFILE_TURBOJPEG_NOEXT}.lib ${LIBDIR}
			COMMAND ${CMAKE_COMMAND} -E copy_if_different ${LIBFILE_TURBOJPEG_LIB} ${LIBDIR}
			COMMAND ${CMAKE_COMMAND} -E copy_if_different ${LIBFILE_JPEG} ${BINDIR}
			COMMAND ${CMAKE_COMMAND} -E copy_if_different ${LIBFILE_JPEG_LIBDYN} ${LIBDIR}
	)
elseif(NOT APPLE)
	ExternalProject_Add(project_${TARGET_JPEG}
		URL ${URL_JPEG}
		URL_MD5 ${URL_MD5_JPEG}
		CMAKE_ARGS ${COMMON_CMAKE_ARGS_JPEG} -DCMAKE_INSTALL_PREFIX=${DESTINATION_PATH}
		BUILD_COMMAND ${PARALLEL_MAKE} SIZEOF_SIZE_T=8
		BUILD_IN_SOURCE 1
		INSTALL_COMMAND make install
	)
endif()
